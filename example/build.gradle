apply plugin: 'com.android.application'

def getProps(filename) {
    def propsFile = rootProject.file(filename)
    def props = new Properties()
    props.load(new FileInputStream(propsFile))
    return props
}

android {
    compileSdkVersion 30
    defaultConfig {
        applicationId "money.zumo.example"
        minSdkVersion 28
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        signingConfig signingConfigs.debug
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildTypes {
        staging {
            getProps('staging.properties').each { p ->
                buildConfigField 'String', p.key, p.value
            }
            applicationIdSuffix ".debug"
            matchingFallbacks = ['debug']
            debuggable true
        }

        sandbox {
            getProps('sandbox.properties').each { p ->
                buildConfigField 'String', p.key, p.value
            }
            applicationIdSuffix ".debug"
            matchingFallbacks = ['debug']
            debuggable true
        }

        debug {
            getProps('production.properties').each { p ->
                buildConfigField 'String', p.key, p.value
            }
            applicationIdSuffix ".debug"
            debuggable true
        }

        release {
            getProps('production.properties').each { p ->
                buildConfigField 'String', p.key, p.value
            }
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
    implementation project(':zumokit')
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.10.1'
}
